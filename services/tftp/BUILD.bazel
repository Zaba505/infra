load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")

go_library(
    name = "tftp_lib",
    srcs = ["main.go"],
    embedsrcs = ["config.yaml"],
    importpath = "github.com/Zaba505/infra/services/tftp",
    visibility = ["//visibility:private"],
    deps = [
        "//services/tftp/service",
        "@com_github_z5labs_app//:app",
    ],
)

go_binary(
    name = "tftp",
    embed = [":tftp_lib"],
    pure = "on",
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "tar",
    srcs = [":tftp"],
)

oci_image(
    name = "image",
    base = "@distroless_base",
    entrypoint = ["/tftp"],
    tars = [":tar"],
)

# This is the target that should be released to the target platform
platform_transition_filegroup(
    name = "transitioned_image",
    srcs = [":image"],
    target_platform = select({
        "@platforms//cpu:arm64": "@rules_go//go/toolchain:linux_arm64",
        "@platforms//cpu:x86_64": "@rules_go//go/toolchain:linux_amd64",
    }),
)

# $ bazel build //services/tftp:tarball
# $ podman load --input $(bazel cquery --output=files //services/tftp:tarball)
# $ podman run --rm -eTFTP_PORT=8000 -eTFTP_BLOCK_SIZE=4096 -eTFTP_RETRIES=2 -eTFTP_TIMEOUT=10s -eGITHUB_TOKEN=abc ghcr.io/zaba505/infra/tftp:latest
oci_tarball(
    name = "tarball",
    image = ":image",
    repo_tags = ["ghcr.io/zaba505/infra/tftp:latest"],
)
